module StateTest;


import Minilib.Monad.Iden;
import Minilib.Monad.State;
import Minilib.Monad.Error;
import Minilib.Monad.IO;
import Minilib.Text.StringEx;
import Minilib.Testing.UnitTest;

test_state_functor: TestCase;
test_state_functor = (
    make_test("test_state_functor") $ |_|
    let sm = make_state_monad(|s| (s, 2));
    let sm = sm.map(add(1));
    let sm = sm.map(to_string);
    assert_equal("eq", ("abc", "3"), sm.run_state("abc"))
);

test_state_monad: TestCase;
test_state_monad = (
    make_test("test_state_monad") $ |_|
    let get_elt = |i| (
        let a = *get_state;
        pure $ a.@(i)
    );
    let set_elt = |i, elt| (
        let a = *get_state;
        let a = a.set(i, elt);
        put_state(a)
    );
    let sm = do {
        set_elt(1, 4);;
        let x = *get_elt(0);
        let y = *get_elt(1);
        pure $ x + y
    };
    assert_equal("eq", ([2, 4], 6), sm.run_state([2, 3]))
);

test_mod_state: TestCase;
test_mod_state = (
    make_test("test_mod_state") $ |_|
    let sm = State::mod_state(|s|
        let s = assert_unique(|_| "state is not unique!", s);
        s.set(0, 42)
    );
    assert_equal("eq", ([42, 1, 2], ()), sm.run_state([0, 1, 2]))
);

test_eval_exec_state_t: TestCase;
test_eval_exec_state_t = (
    make_test("test_eval_exec_state_t") $ |_|
    let sm: StateT String (Result ErrMsg) I64 = do {
        let s = *get_state;
        let a = s.get_size;
        put_state("end");;
        pure $ a
    };
    assert_equal("eval_state_t", ok $ 5, sm.eval_state_t("start"));;
    assert_equal("exec_state_t", ok $ "end", sm.exec_state_t("start"));;
    pure()
);

test_eval_exec_state: TestCase;
test_eval_exec_state = (
    make_test("test_eval_exec_state") $ |_|
    let sm: State String I64 = do {
        let s = *get_state;
        let a = s.get_size;
        put_state("end");;
        pure $ a
    };
    assert_equal("eval_state", 5, sm.eval_state("start"));;
    assert_equal("exec_state", "end", sm.exec_state("start"));;
    pure()
);

test_map_state_t: TestCase;
test_map_state_t = (
    make_test("test_map_state_t") $ |_|
    let sm: StateT I64 Iden I32 = do {
        let i64 = *get_state;
        put_state(i64 + 1);;
        pure $ i64.to_I32
    };
    let sm: StateT I64 IOFail String = sm.map_state_t(|ma|
        pure $ ma.get.map(to_string)
    );
    let (i64, str) = *sm.run_state_t(42);
    assert_equal("i64", 43, i64);;
    assert_equal("str", "42", str);;
    pure()
);

test_state_result_error: TestCase;
test_state_result_error = (
    make_test("test_state_result_error") $ |_|
    let sm: StateT I64 (Result ErrMsg) I64 = do {
        let i = *get_state;
        put_state(i + 10);;
        if i == 1 { error $ "err1" };
        if i == 2 { error $ "err2" };
        pure(i + 20)
    };
    assert_equal("err1", err $ "err1", sm.run_state_t(1));;
    assert_equal("err2", err $ "err2", sm.run_state_t(2));;
    assert_equal("ok3", ok $ (13, 23), sm.run_state_t(3));;
    let handler: String -> StateT I64 (Result ErrMsg) I64 = |errmsg| (
        let i = *get_state;     // state is not modified when the error is caught
        put_state(i + 100);;
        if i == 1 { pure(999) };
        error $ errmsg
    );
    assert_equal("catch err1", ok $ (101, 999), sm.catch(handler).run_state_t(1));;
    assert_equal("catch err2", err $ "err2", sm.catch(handler).run_state_t(2));;
    assert_equal("catch ok3", ok $ (13, 23), sm.catch(handler).run_state_t(3));;
    pure()
);

test_state_iofail_error: TestCase;
test_state_iofail_error = (
    make_test("test_state_iofail_error") $ |_|
    let sm: StateT (Array I64) IOFail I64 = do {
        let arr: Array I64 = *get_state;
        let arr = arr.push_back(2);
        if arr.get_size != 2 { error $ "err" };
        put_state(arr);;
        pure $ arr.to_iter.sum
    };
    assert_equal("eq1", ok $ ([1, 2], 3), *sm.run_state_t([1]).to_result.lift);;
    assert_equal("eq2", err $ "err", *sm.run_state_t([1, 2]).to_result.lift);;
    let handler: String -> StateT (Array I64) IOFail I64 = |errmsg| (
        let arr: Array I64 = *get_state; // state is not modified when the error is caught
        put_state(arr.push_back(99));;
        pure $ 99
    );
    let sm2 = do {
        let i = *sm.catch(handler);
        pure $ i + 100
    };
    assert_equal("eq1", ok $ ([1, 2], 103), *sm2.run_state_t([1]).to_result.lift);;
    assert_equal("eq2", ok $ ([1, 2, 99], 199), *sm2.run_state_t([1, 2]).to_result.lift);;
    pure()
);

test_state_io_lift_io: TestCase;
test_state_io_lift_io = (
    make_test("test_state_io_lift_io") $ |_|
    let inner: IO I64 = pure $ 42;
    let sm: StateT (Array I64) IO () = do {
        State::mod_state(push_back(*inner.lift_io))
    };
    assert_equal("eq", ([1, 42], ()), *sm.run_state_t([1]).lift)
);

test_state_iofail_lift_io: TestCase;
test_state_iofail_lift_io = (
    make_test("test_state_iofail_lift_io") $ |_|
    let inner_io: IO I64 = do {
        //println("inner_io");;
        pure $ 42
    };
    let inner_iofail: IOFail I64 = do {
        //println("inner_iofail").lift;;
        pure $ 57
    };
    let sm: StateT (Array I64) IOFail () = do {
        State::mod_state(
            // NOTE: `inner_io` is performed before `inner_iofail`.
            // After that, function composition (`<<`) is calculated, so the result is 57 and 42.
            push_back(*inner_io.lift_io) << push_back(*inner_iofail.lift_iofail)
        )
    };
    assert_equal("eq", ok $ ([1, 57, 42], ()), *sm.run_state_t([1]).to_result.lift)
);

type Book = unbox struct {
    title: String
};

type Shelf = unbox struct {
    books: Array Book
};

test_lens_state_t: TestCase;
test_lens_state_t = (
    make_test("test_lens_state_t") $ |_|
    let sm: StateT String (Result ErrMsg) I64 = do {
        let s = *get_state;
        let a = s.get_size;
        put_state("<" + s + ">");;
        pure $ a
    };
    let shelf = Shelf {
        books: [ Book { title: "a book" }, Book { title: "another book" } ]
    };
    let sm: StateT Shelf (Result ErrMsg) I64 = sm.lens_state_t(Book::act_title >> Array::act(0) >> Shelf::act_books);
    assert_equal("eval_state_t", ok $ 6, sm.eval_state_t(shelf));;
    assert_equal("exec_state_t", ok $ "<a book>", sm.exec_state_t(shelf).map(@books >> @(0) >> @title));;
    pure()
);

test_lens_state: TestCase;
test_lens_state = (
    make_test("test_lens_state_t") $ |_|
    let sm: State String I64 = do {
        let s = *get_state;
        let a = s.get_size;
        put_state("<" + s + ">");;
        pure $ a
    };
    let sm: State Book I64 = sm.lens_state(Book::act_title);
    let book = Book { title: "a book" };
    assert_equal("eval_state", 6, sm.eval_state(book));;
    assert_equal("exec_state", "<a book>", sm.exec_state(book).@title);;
    pure()
);

main: IO ();
main = (
    [
        test_state_functor,
        test_state_monad,
        test_mod_state,
        test_eval_exec_state_t,
        test_eval_exec_state,
        test_map_state_t,
        test_state_result_error,
        test_state_iofail_error,
        test_state_io_lift_io,
        test_state_iofail_lift_io,
        test_lens_state_t,
        test_lens_state,
        TestCase::empty
    ]
    .run_test_driver
);


